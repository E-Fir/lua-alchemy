<?xml version="1.0" encoding="utf-8"?>
<mx:HDividedBox xmlns:mx="http://www.adobe.com/2006/mxml"
  creationComplete="onCreationComplete()">
  <mx:Script>
    <![CDATA[
      import mx.effects.Move;
      import mx.containers.*;
      import mx.controls.*;
      import luaAlchemy.lua_wrapper;
      import cmodule.lua_wrapper.CLibInit;
      import flash.net.FileReference;
      import flash.net.FileFilter;

      import flash.events.IOErrorEvent;
      import flash.events.Event;

      import flash.utils.ByteArray;

      private var luaState:uint;

      [Bindable]
      private var showClearStackLuaState:Boolean = false;

      //FileReference Class well will use to load data
      private var fr:FileReference;

      //File types which we want the user to open
      private static const FILE_TYPES:Array = [new FileFilter("All Files", "*.*")];

      private function onCreationComplete():void
      {
        resetLuaState();
        importClassesForLua();
      }

      private function resetLuaState():void
      {
        if (luaState)
          lua_wrapper.luaClose(luaState);

        luaState = lua_wrapper.luaInitilizeState();
        lua_wrapper.setGlobal(luaState, "canvas", luaCanvas);
        lua_wrapper.setGlobal(luaState, "output", outputTA);
      }

      private function onRunClicked(event:MouseEvent):void
      {
        if (clearBeforeRunCB.selected)
          outputTA.text = "";

        if (resetBeforeRunCB.selected)
        {
          luaCanvas.removeAllChildren();
          resetLuaState();
        }

        var stack:Array = lua_wrapper.luaDoString(luaState, luaCodeTA.text);
        if (stack.length > 0)
            outputTA.text = outputTA.text + "\n=== Return values ===\n" + stack.join("\n");
      }

      private function onLoadClicked(event:MouseEvent):void
      {
        fr = new FileReference();
        fr.addEventListener(Event.SELECT, onFileSelect);
        fr.addEventListener(Event.CANCEL,onCancel);
        fr.browse(FILE_TYPES);
      }

      private function onFileSelect(e:Event):void
      {
        fr.addEventListener(Event.COMPLETE, onLoadComplete);
        fr.addEventListener(IOErrorEvent.IO_ERROR, onLoadError);
        fr.load();
      }

      private function onLoadComplete(e:Event):void
      {
        var data:ByteArray = fr.data;
        luaCodeTA.text = data.readUTFBytes(data.bytesAvailable);
        fr = null;
      }

      private function onLoadError(e:IOErrorEvent):void
      {
        trace("Error loading file : " + e.text);
      }

      private function onCancel(e:Event):void
      {
        trace("File Browse Canceled");
        fr = null;
      }

      private function onSaveClicked(event:MouseEvent):void
      {
        fr = new FileReference();
        fr.addEventListener(Event.COMPLETE, onFileSave);
        fr.addEventListener(Event.CANCEL,onCancel);
        fr.addEventListener(IOErrorEvent.IO_ERROR, onSaveError);
        fr.save(luaCodeTA.text, "LuaAlchemy.lua");
      }

      private function onFileSave(e:Event):void
      {
        trace("File Saved");
        fr = null;
      }

      private function onSaveError(e:IOErrorEvent):void
      {
        trace("Error Saving File : " + e.text);
        fr = null;
      }

      /**
      * When you compile in the Flex library statically, the linker only includes
      * classes you are actually using.  Only the classes used would be available
      * for the Lua script to create.  So to ensure the script has access to the
      * classes I wanted to create, I declared one of them so it would be included.
      */
      private function importClassesForLua():void
      {
        var accordion:Accordion
        var applicationControlBar:ApplicationControlBar
        var box:Box
        var boxDirection:BoxDirection
        var canvas:Canvas
        var controlBar:ControlBar
        var dividedBox:DividedBox
        var form:Form
        var formHeading:FormHeading
        var formItem:FormItem
        var formItemDirection:FormItemDirection
        var grid:Grid
        var gridItem:GridItem
        var gridRow:GridRow
        var hBox:HBox
        var hDividedBox:HDividedBox
        var panel:Panel
        var tabNavigator:TabNavigator
        var tile:Tile
        var tileDirection:TileDirection
        var titleWindow:TitleWindow
        var vBox:VBox
        var vDividedBox:VDividedBox
        var viewStack:ViewStack

        var alert:Alert
        var button:Button
        var buttonBar:ButtonBar
        var buttonLabelPlacement:ButtonLabelPlacement
        var checkBox:CheckBox
        var colorPicker:ColorPicker
        var comboBase:ComboBase
        var comboBox:ComboBox
        var dataGrid:DataGrid
        var dateChooser:DateChooser
        var dateField:DateField
        var formItemLabel:FormItemLabel
        var horizontalList:HorizontalList
        var hRule:HRule
        var hScrollBar:HScrollBar
        var hSlider:HSlider
        var image:Image
        var label:Label
        var linkBar:LinkBar
        var linkButton:LinkButton
        var list:List
        var menu:Menu
        var menuBar:MenuBar
        var navBar:NavBar
        var numericStepper:NumericStepper
        var popUpButton:PopUpButton
        var popUpMenuButton:PopUpMenuButton
        var progressBar:ProgressBar
        var progressBarDirection:ProgressBarDirection
        var progressBarLabelPlacement:ProgressBarLabelPlacement
        var progressBarMode:ProgressBarMode
        var radioButton:RadioButton
        var radioButtonGroup:RadioButtonGroup
        var richTextEditor:RichTextEditor
        var spacer:Spacer
        var sWFLoader:SWFLoader
        var tabBar:TabBar
        var text:Text
        var textArea:TextArea
        var textInput:TextInput
        var tileList:TileList
        var toggleButtonBar:ToggleButtonBar
        var toolTip:ToolTip
        var tree:Tree
        var videoDisplay:VideoDisplay
        var vRule:VRule
        var vScrollBar:VScrollBar
        var vSlider:VSlider

        var timer:Timer
        var move:Move
      }
    ]]>
  </mx:Script>
  <mx:Panel title="Lua" width="100%" height="100%" layout="vertical"
    paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
    <mx:TextArea id="luaCodeTA" width="100%" height="100%" condenseWhite="false" text="{defaultLua}"
      paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5"/>
    <mx:HBox horizontalAlign="center">
      <mx:Button id="runBtn" label="Run!" click="onRunClicked(event)" toolTip="Run the Lua code"/>
      <mx:Button label="Reset" click="resetLuaState()" toolTip="Reset the Lua Context"
        visible="{showClearStackLuaState}" includeInLayout="{showClearStackLuaState}"/>
      <mx:Spacer width="25"/>
      <mx:Button label="Load" click="onLoadClicked(event)" toolTip="Load Lua code from file"/>
      <mx:Button label="Save" click="onSaveClicked(event)" toolTip="Save Lua code to file"/>
      <mx:Spacer width="25"/>
      <mx:Button label="Clear" click="luaCodeTA.text = ''" toolTip="Clear the Lua code"/>
      <mx:Button label="Default" click="luaCodeTA.text = defaultLua" toolTip="Set Lua code to default"/>
    </mx:HBox>
    <mx:CheckBox id="resetBeforeRunCB" label="Reset Lua before Run" selected="true"
      toolTip="If this is unchecked, code will accumulate in Lua and be rerun."
      visible="{showClearStackLuaState}" includeInLayout="{showClearStackLuaState}"/>
  </mx:Panel>
  <mx:Panel title="Alchemy" layout="vertical" width="100%" height="100%"
    paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
    <mx:VDividedBox width="100%" height="100%">
      <mx:Panel title="Canvas" width="100%" height="100%" layout="vertical">
        <mx:Canvas id="luaCanvas" width="100%" height="100%"/>
      </mx:Panel>
      <mx:Panel title="Output" width="100%" height="100%" layout="vertical">
        <mx:TextArea id="outputTA" editable="false" width="100%" height="100%"
          paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5"/>
        <mx:HBox width="100%" visible="{showClearStackLuaState}" includeInLayout="{showClearStackLuaState}">
          <mx:Button label="Clear" click="outputTA.text = ''" toolTip="Clear the displayed Lua return stack"
            visible="{showClearStackLuaState}" includeInLayout="{showClearStackLuaState}"/>
          <mx:CheckBox id="clearBeforeRunCB" label="Clear Stack before Run" selected="true"
            toolTip="If checked, the stack result will be cleared before running"
            visible="{showClearStackLuaState}" includeInLayout="{showClearStackLuaState}"/>
        </mx:HBox>
      </mx:Panel>
    </mx:VDividedBox>
    <!-- TODO would like to redirect stdout,stderr here
    <mx:Panel title="Lua stdout, stderr, stdin" width="100%" height="100%" includeInLayout="false" visible="false">
      <mx:UIComponent id="luaStdioTA" width="100%" height="100%"/>
    </mx:Panel>
    -->
  </mx:Panel>

  <mx:String id="defaultLua">
<![CDATA[print = function(...) as3.set(output, "text", as3.get(output, "text") .. table.concat({...}, "\t") .. "\n") end
print("hello from Lua")

local alertClass = as3.class("mx.controls::Alert")
local vbox = as3.new("mx.containers::VBox")
local label = as3.new("mx.controls::Label")
local input = as3.new("mx.controls::TextInput")
local button = as3.new("mx.controls::Button")
local box = as3.new("mx.containers::Canvas")

as3.set(label, "text", "Name:")

as3.set(button, "label", "Say Hello");
as3.call(button, "addEventListener", "click",
  function (e)
      as3.call(alertClass, "show", "Hello " .. as3.get(input, "text"), as3.type(e))
  end)

as3.set(box, "width", 20)
as3.set(box, "height", 20)
as3.set(box, "x", as3.get(canvas, "width") - 20)
as3.set(box, "y", as3.get(canvas, "height") - 20)
as3.call(box, "setStyle", "backgroundColor", "blue")

as3.call(vbox, "addChild", label)
as3.call(vbox, "addChild", input)
as3.call(vbox, "addChild", button)

as3.call(canvas, "addChild", box)
as3.call(canvas, "addChild", vbox)

local move = as3.new("mx.effects::Move")
as3.set(move, "target", box)
as3.set(move, "duration", 5000)

function movebox()
  as3.call(move, "end")
  as3.set(move, "xTo", math.random(0, as3.get(canvas, "width") - 20))
  as3.set(move, "yTo", math.random(0, as3.get(canvas, "height") - 20))
  as3.call(move, "play")
end

movebox()

local timer = as3.new("flash.utils::Timer", 5000)
as3.call(timer, "addEventListener", "timer", movebox)
as3.call(timer, "start")

return
  true,
  "Hello from Lua",
  42,
  5.6+13,
  math.pi,
  as3.new("String", "AS3 string"),
  as3.new("Number", 128)]]>
  </mx:String>

</mx:HDividedBox>
